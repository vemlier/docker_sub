#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Long qw(:config no_ignore_case);
use FindBin qw($Bin $Script);

# =============== Variables ===============
my $script_path         = "$Bin";
my $pwd                 = $ENV{"PWD"};
my $user                = $ENV{"USER"};
my $hostname            = $ENV{"HOSTNAME"};
my $library_information = "$Bin/library.txt";
my $docker_image =
  exists $ENV{'DEEPONE_DOCKER_IMAGE'}
  ? $ENV{'DEEPONE_DOCKER_IMAGE'}
  : "sklearn:1.6.1";
my $job_name = "myjob";
my $queue;
my $host;
my $cpu_num = exists $ENV{'DEEPONE_CPU_NUM'} ? $ENV{'DEEPONE_CPU_NUM'} : 1;
my $gpu_num = exists $ENV{'DEEPONE_GPU_NUM'} ? $ENV{'DEEPONE_GPU_NUM'} : 0;
my $gpu_mode = "exclusive_process";
my $shared_mode;
my $batch_mode;
my $jupyter_mode;
my $debug_mode;
my $docker_cmd;
my $bsub;
my $batch = "lsf.sub";
my $system_cmd;
my @python_versions;
my @docker_images;
my @library_version_list;

# =============== Read library.txt file ===============
my $config = do $library_information;
for my $library_name ( keys %{ $config->{'libraries'} } ) {
    push @docker_images, $library_name;
}

for my $i ( @{ $config->{'python'} } ) {
    push @python_versions, $i;
}

# =============== Get Options ===============
GetOptions(
    'b|batch'      => \$batch_mode,
    'J|job_name=s' => \$job_name,
    'q|queue=s'    => \$queue,
    'm|machine=s'  => \$host,
    'n|cpu=i'      => \$cpu_num,
    'g|gpu=i'      => \$gpu_num,
    's|shared'     => \$shared_mode,
    'i|image=s'    => \$docker_image,
    'j|jupyter'    => \$jupyter_mode,
    'd|debug'      => \$debug_mode,
    'h|help'       => sub { usage() }
) or usage();

for ( my $i = 0 ; $i < @ARGV ; $i++ ) {

    #========== docker.lsf list ==========
    if ( $ARGV[$i] eq 'list' ) {

        for my $library_name (@docker_images) {
            print "===============\n";
            print $config->{libraries}{$library_name}{description}, "\n";
            print "===============\n";
            print "$library_name: ";

            for my $library_version (
                @{ $config->{libraries}{$library_name}{versions} } )
            {
                print " $library_version ";
            }

            print "\n===============\n\n";
        }
        exit 0;

        #========== docker.lsf exec ==========
    }
    elsif ( $ARGV[$i] eq 'exec' ) {

        my $container = $ARGV[ $i + 1 ]
          or die "ERROR: container name required\n";
        chomp( my $exec_host =
`bjobs -o "exec_host" $container | sed '1d' | awk -F: {'print \$1'}`
        );
        if ( $exec_host ne "" ) {
            if ( $exec_host eq $hostname ) {
                $system_cmd = "docker exec -it $container /bin/bash";
            }
            else {
                $system_cmd =
                  "ssh -t $exec_host docker exec -it $container /bin/bash";
            }
        }
        else {
            die "$container, Not your container\n";
        }
        print "Executing: $system_cmd\n";
        system($system_cmd) == 0 or die "docker exec failed\n";
        exit 0;

        #========== docker.lsf kill ==========
    }
    elsif ( $ARGV[$i] eq 'kill' ) {
        my $container = $ARGV[ $i + 1 ]
          or die "ERROR: container name required\n";
        chomp( my $exec_host = `bjobs -o "exec_host" $container | sed '1d'` );
        if ( $exec_host ne "" ) {
            if ( $exec_host eq $hostname ) {
                $system_cmd = "docker rm -f $container";
            }
            else {
                $system_cmd = "ssh $exec_host docker rm -f $container";
            }
        }
        else {
            die "$container, Not your container\n";
        }
        print "Executing: $system_cmd\n";
        system($system_cmd) == 0 or die "docker exec failed\n";
        exit 0;
    }
}

# =============== Docker command ===============
if ( !-f $library_information || !-r $library_information ) {
    die "$library_information does not exist\n";
}

$docker_cmd =
"docker run --rm -v $pwd:$pwd -w $pwd --name \$LSB_JOBID --hostname d_\$HOSTNAME ";
$docker_cmd .= "-v /etc/passwd:/etc/passwd -v /etc/group:/etc/group ";
$docker_cmd .= "--gpus $gpu_num "  if ( $gpu_num > 0 );
$docker_cmd .= "-it "              if ( !defined $batch_mode );
$docker_cmd .= "-p \$PORT:\$PORT " if ( defined $jupyter_mode );
$docker_cmd .= "--user \$(id -u):\$(id -g) ";

# Manual Port Option
#$docker_cmd .= "-p 8888:\$PORT "    if ( defined $jupyter_mode );
$docker_cmd .= "$docker_image ";

if ($jupyter_mode) {
    $docker_cmd .= "bash -c \"exec jupyter notebook \\
    --port=\$PORT \\
    --ip=0.0.0.0 \\
    --allow-root \\
    --NotebookApp.token='' \\
    --NotebookApp.custom_display_url=http://\$IP:\$PORT\""
}
elsif (@ARGV) {
    $docker_cmd .= "bash -c \"@ARGV\"";
}
else {
    if ($batch_mode) {
        $docker_cmd .= "tail -f /dev/null";
    }
    else {
        $docker_cmd .= "bash -c \"/bin/bash --login\"";
    }
}

$gpu_mode = "shared" if ($shared_mode);

# =============== BSUB script ===============
$bsub .= "#!/usr/bin/env bash\n";
$bsub .= "#BSUB -J \"$job_name\"\n";
$bsub .= "#BSUB -q $queue\n" if ( defined $queue );
$bsub .= "#BSUB -m $host\n" if ( defined $host );
$bsub .= "#BSUB -n $cpu_num\n";
$bsub .= "#BSUB -gpu \"num=$gpu_num:mode=$gpu_mode\"\n" if ( $gpu_num > 0 );

if ($batch_mode) {

    # Batch Job
    $bsub .= "#BSUB -o %J.lslog\n";
    $bsub .= "#BSUB -e %J.lslog\n";

    # for Jupyter notebook url
    #  $bsub .= "#BSUB -e %J.lserror\n";
}
else {
    # Interactive Job
    $bsub .= "#BSUB -Is\n";
}

if ($jupyter_mode) {
    $bsub .= << 'EOF';

IP=$(hostname -I | awk '{print $1}')
PORT=$(echo $LSB_JOBID | tail -c 5)
while [ ${#PORT} -lt 4 ]; do
    PORT="9$PORT"
done
EOF
}

$bsub .= "\n";
$bsub .= "HOSTNAME=\`hostname\`\n";
$bsub .= "\n";
$bsub .= $docker_cmd;
$bsub .= "\n";

if ($debug_mode) {
    print "\n";
    print "=====Your Enviroment variables=====\n";
    print "DEEPONE_DOCKER_IMAGE: $ENV{'DEEPONE_DOCKER_IMAGE'}\n"
      if exists $ENV{'DEEPONE_DOCKER_IMAGE'};
    print "DEEPONE_GPU_NUM: $ENV{'DEEPONE_GPU_NUM'}\n"
      if exists $ENV{'DEEPONE_GPU_NUM'};
    print "=====Your Enviroment variables=====\n";
    print "\n";
    print "=====Docker command=====\n";
    print $docker_cmd, "\n";
    print "=====Docker command=====\n";
    print "\n";

    open my $BSUB, ">$batch" or die "Cannot open $batch $!";
    print $BSUB $bsub;
    close $BSUB;
}
else {
    open my $BSUB, "| bsub" or die "Cannot open $batch $!";
    print $BSUB $bsub;
    close $BSUB;
}

sub usage {
    print << "EOF";
=========================================================
docker.lsf 1.0
=========================================================
Usage: docker.lsf -n <cpu_num> -g <gpu_num> -q <submit_queue> -m <submit_host> -i <docker_image>
       docker.lsf list
       docker.lsf exec <JobID>
       docker.lsf kill <JobID>

  -b  : Batch mode ( Default: Interactive )
  -J  : Job name  ( Default: myjob)
  -q  : Sumit queue
  -m  : Sumit host
  -n  : Number of cpu ( Default: 1, DEEPONE_CPU_NUM )
  -g  : Number of gpu ( Default: 0, DEEPONE_GPU_NUM )
  -s  : Use GPU shared mode
  -j  : Jupyter notebook mode
  -i  : Docker image ( @docker_images ) (DEEPONE_DOCKER_IMAGE)
  -d  : Run debug mode

  list          : list docker images
  exec <JobID>  : Go to docker container ( Need ssh ) 
  kill <JobID>  : Kill docker container ( Need ssh ) 
=========================================================
EOF

    exit 0;
}
